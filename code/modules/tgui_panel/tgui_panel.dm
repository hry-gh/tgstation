/*!
 * Copyright (c) 2020 Aleksej Komarov
 * SPDX-License-Identifier: MIT
 */

/**
 * tgui_panel datum
 * Hosts tgchat and other nice features.
 */
#define TGPANEL_POPUP_WINDOW "tgui_panel_popup"

/datum/tgui_panel
	var/client/client
	var/datum/tgui_window/window
	var/broken = FALSE
	var/initialized_at
	var/current_layout
	/// Each client notifies on protected playback, so this prevents spamming admins.
	var/static/admins_warned = FALSE

/datum/tgui_panel/New(client/client, id)
	src.client = client
	window = new(client, id)
	window.subscribe(src, PROC_REF(on_message))

/datum/tgui_panel/Del()
	window.unsubscribe(src)
	window.close()
	return ..()

/**
 * public
 *
 * TRUE if panel is initialized and ready to receive messages.
 */
/datum/tgui_panel/proc/is_ready()
	return !broken && window.is_ready()

/**
 * public
 *
 * Initializes tgui panel.
 */
/datum/tgui_panel/proc/initialize(force = FALSE)
	set waitfor = FALSE
	// Minimal sleep to defer initialization to after client constructor
	sleep(1 TICKS)
	initialized_at = world.time
	// Perform a clean initialization
	window.initialize(
		strict_mode = TRUE,
		assets = list(
			get_asset_datum(/datum/asset/simple/tgui_panel),
		))
	window.send_asset(get_asset_datum(/datum/asset/simple/namespaced/fontawesome))
	window.send_asset(get_asset_datum(/datum/asset/simple/namespaced/tgfont))
	window.send_asset(get_asset_datum(/datum/asset/spritesheet_batched/chat))
	// Other setup
	request_telemetry()
	addtimer(CALLBACK(src, PROC_REF(on_initialize_timed_out)), 5 SECONDS)
	window.send_message("testTelemetryCommand")

/**
 * private
 *
 * Called when initialization has timed out.
 */
/datum/tgui_panel/proc/on_initialize_timed_out()
	// Currently does nothing but sending a message to old chat.
	SEND_TEXT(client, span_userdanger("Failed to load fancy chat, click <a href='byond://?src=[REF(src)];reload_tguipanel=1'>HERE</a> to attempt to reload it."))

/**
 * private
 *
 * Callback for handling incoming tgui messages.
 */
/datum/tgui_panel/proc/on_message(type, payload)
	if(type == "ready")
		broken = FALSE
		window.send_message("update", list(
			"config" = list(
				"client" = list(
					"ckey" = client.ckey,
					"address" = client.address,
					"computer_id" = client.computer_id,
				),
				"window" = list(
					"locked" = FALSE,
				),
			),
		))
		send_player_info()
		send_hotkey_mode()
		return TRUE

	if(type == "audio/setAdminMusicVolume")
		client.admin_music_volume = payload["volume"]
		return TRUE

	if(type == "audio/protected")
		if(!admins_warned)
			message_admins(span_notice("Audio returned a protected playback error, likely due to being copyrighted."))
			admins_warned = TRUE
			addtimer(VARSET_CALLBACK(src, admins_warned, FALSE), 10 SECONDS)
		return TRUE

	if(type == "telemetry")
		analyze_telemetry(payload)
		return TRUE

	if(type == "verbs/request_verbs")
		client.init_verbs()
		return TRUE

	if(type == "panel/toggle_layout")
		winset(client, "map", "focus=true")
		var/current = client.prefs.read_preference(/datum/preference/choiced/tgpanel_layout)
		var/next
		switch(current)
			if(TGPANEL_ONMAP)
				next = TGPANEL_PANEL
			if(TGPANEL_PANEL)
				next = TGPANEL_ONMAP
			else
				next = TGPANEL_ONMAP
		log_tgui(client, "panel/toggle_layout: [current] -> [next]", context = "tgui_panel")
		client.prefs.update_preference(GLOB.preference_entries[/datum/preference/choiced/tgpanel_layout], next)
		return TRUE

	if(type == "verbs/request_typepaths")
		var/parent_text = payload["parent"]
		var/browse_type = text2path(parent_text)
		if(isnull(browse_type))
			browse_type = /datum
		var/list/children = list()
		for(var/child_type in typesof(browse_type))
			if(child_type == browse_type)
				continue
			// Only include direct children (one level deeper)
			var/child_text = "[child_type]"
			var/parent_len = length(parent_text || "/datum")
			var/remainder = copytext(child_text, parent_len + 1)
			if(findtext(remainder, "/", 2))
				continue
			children += child_text
		window.send_message("verbs/typepaths", list("parent" = parent_text, "paths" = children))
		return TRUE

	if(type == "verbs/request_targets")
		var/verb_path = text2path(payload["verb_type"])
		if(!verb_path)
			return TRUE
		if(!(verb_path in client.verbs) && !(client.mob && (verb_path in client.mob.verbs)))
			return TRUE

		var/list/arg_list
		var/datum/verb_metadata/meta = SSverbs.verbs_by_verb_path[verb_path]
		if(meta)
			arg_list = meta.arguments
		else
			var/datum/admin_verb/av = SSadmin_verbs.admin_verbs_by_verb_path[verb_path]
			if(av)
				arg_list = av.arguments
		if(!length(arg_list))
			return TRUE
		var/datum/verb_arg_metadata/entity_arg
		for(var/datum/verb_arg_metadata/arg in arg_list)
			if(arg.arg_type & (VERB_ARG_TYPE_MOB | VERB_ARG_TYPE_OBJ | VERB_ARG_TYPE_TURF | VERB_ARG_TYPE_AREA | VERB_ARG_TYPE_DATUM | VERB_ARG_TYPE_ATOM))
				entity_arg = arg
				break
		if(!entity_arg)
			return TRUE
		var/list/target_data = list()
		var/list/source_atoms = get_targets_for_arg(entity_arg)
		for(var/atom/target in source_atoms)
			target_data += list(list("name" = "[target]", "ref" = REF(target)))
		window.send_message("verbs/targets", list("targets" = target_data))
		return TRUE

	if(type == "verbs/invoke")
		var/verb_path = text2path(payload["verb_type"])
		if(!verb_path)
			return TRUE

		var/datum/admin_verb/admin_meta = SSadmin_verbs.admin_verbs_by_verb_path[verb_path]
		if(admin_meta)
			var/list/resolved_args = resolve_invoke_args(payload["args"], admin_meta.arguments)
			SSadmin_verbs.dynamic_invoke_verb(client, admin_meta.type, resolved_args)
			return TRUE
		var/target = resolve_verb_target(verb_path)
		if(!target)
			return TRUE
		var/datum/verb_metadata/meta = SSverbs.verbs_by_verb_path[verb_path]
		if(meta)
			var/list/resolved_args = resolve_invoke_args(payload["args"], meta.arguments)
			call(target, meta.body_path)(resolved_args)
		else
			call(target, verb_path)()
		return TRUE

	if(type == "panel/bounds")
		var/x = payload["x"]
		var/y = payload["y"]
		var/w = payload["w"]
		var/h = payload["h"]
		var/datum/hud/hud = client.mob?.hud_used
		if(!hud)
			return TRUE
		// Get map element size and view-size to compute letterbox offset
		var/map_view_size_raw = winget(client, "mapwindow.map", "view-size")
		var/map_elem_size_raw = winget(client, "mapwindow.map", "size")
		var/list/view_parts = splittext("[map_view_size_raw]", "x")
		var/list/elem_parts = splittext("[map_elem_size_raw]", "x")
		if(length(view_parts) < 2 || length(elem_parts) < 2)
			return TRUE
		var/view_w = text2num(view_parts[1])
		var/view_h = text2num(view_parts[2])
		var/elem_w = text2num(elem_parts[1])
		var/elem_h = text2num(elem_parts[2])
		hud.cached_map_view_size = list(view_w, view_h)
		// Letterbox offset: the map rendering is centered within the element
		var/offset_x = (elem_w - view_w) / 2
		var/offset_y = (elem_h - view_h) / 2
		// Debug logging
		var/browser_pos_raw = winget(client, "browseroutput", "pos")
		var/browser_size_raw = winget(client, "browseroutput", "size")
		to_chat(world, span_boldannounce("DEBUG PANEL/BOUNDS: map.size=[map_elem_size_raw] map.view-size=[map_view_size_raw] letterbox=[offset_x],[offset_y]"))
		to_chat(world, span_boldannounce("DEBUG PANEL/BOUNDS: browser.pos=[browser_pos_raw] browser.size=[browser_size_raw] tgui_chat=[x],[y],[w],[h]"))
		if(!isnull(x) && !isnull(y) && !isnull(w) && !isnull(h))
			// Adjust chat coords from mapwindow space to map rendering space
			var/adj_x = x - offset_x
			var/adj_y = y - offset_y
			// Clamp to the visible map rendering area
			var/adj_right = min(adj_x + w, view_w)
			var/adj_bottom = min(adj_y + h, view_h)
			adj_x = max(adj_x, 0)
			adj_y = max(adj_y, 0)
			var/adj_w = adj_right - adj_x
			var/adj_h = adj_bottom - adj_y
			if(adj_w > 0 && adj_h > 0)
				hud.displace_hud_for_chat(list(adj_x, adj_y, adj_w, adj_h))
			else
				hud.displace_hud_for_chat(null)
		else
			hud.displace_hud_for_chat(null)
		return TRUE

	if(type == "requestMetadata")
		send_metadata()
		return TRUE

/datum/tgui_panel/proc/resolve_invoke_args(list/raw_args, list/arg_metadata)
	if(!islist(raw_args))
		raw_args = list()
	var/alist/resolved = alist()
	for(var/datum/verb_arg_metadata/meta in arg_metadata)
		if(!(meta.name in raw_args))
			continue
		var/value = raw_args[meta.name]
		if(meta.arg_type & VERB_ARG_TYPE_NUM)
			value = text2num(value)
		else if(!(meta.arg_type & VERB_ARG_TYPE_TYPEPATH) && istext(value))
			var/located = locate(value)
			if(located)
				value = located
		resolved[meta.name] = value
	return resolved

/datum/tgui_panel/proc/resolve_verb_target(verb_path)
	if(verb_path in client.verbs)
		return client
	if(client.mob && (verb_path in client.mob.verbs))
		return client.mob
	return null

/datum/tgui_panel/proc/get_targets_for_arg(datum/verb_arg_metadata/arg)
	var/list/targets = list()
	switch(arg.source)
		if(VERB_ARG_SOURCE_WORLD)
			if(arg.arg_type & VERB_ARG_TYPE_MOB)
				return GLOB.mob_list
			if(arg.arg_type & VERB_ARG_TYPE_AREA)
				return get_sorted_areas()
			if(arg.arg_type & VERB_ARG_TYPE_TURF)
				for(var/mob/player in GLOB.player_list)
					var/turf/player_turf = get_turf(player)
					if(player_turf)
						targets |= player_turf
				return targets
			if(arg.arg_type & (VERB_ARG_TYPE_OBJ | VERB_ARG_TYPE_DATUM | VERB_ARG_TYPE_ATOM))
				if(client.mob)
					return view(client.view, client.mob)
		if(VERB_ARG_SOURCE_VIEW)
			if(!client.mob)
				return targets
			var/list/visible = view(client.view, client.mob)
			if(arg.arg_type & VERB_ARG_TYPE_MOB)
				for(var/mob/target in visible)
					targets += target
			else if(arg.arg_type & VERB_ARG_TYPE_OBJ)
				for(var/obj/target in visible)
					targets += target
			else if(arg.arg_type & VERB_ARG_TYPE_TURF)
				for(var/turf/target in visible)
					targets += target
			else
				return visible
	return targets

/**
 * public
 *
 * Sends a round restart notification.
 */
/datum/tgui_panel/proc/send_roundrestart()
	window.send_message("roundrestart")

/**
 * public
 *
 * Sends the client's current job, character and saved character names,
 * used for conditional chat highlights.
 */
/datum/tgui_panel/proc/send_player_info()
	window.send_message("player/set", list(
		"job" = client.mob?.mind?.assigned_role?.title,
		"character" = client.prefs?.read_preference(/datum/preference/name/real_name),
		"characters" = client.prefs?.create_character_profiles(),
	))

/**
 * private
 *
 * Sent when a client requests metadata - used for websocket stuff.
 */
/datum/tgui_panel/proc/send_metadata()
	var/static/list/webroot_asset_urls

	var/list/metadata = list(
		"game_version" = GLOB.game_version,
		"server_name" = CONFIG_GET(string/servername),
		"round_id" = GLOB.round_id,
		"map_name" = SSmapping.current_map?.map_name,
		"round_duration" = round(STATION_TIME_PASSED() / 10, 1),
		"gamestate" = SSticker.current_state,
	)
	// if we're using webroot - also pass along the webroot url and such, so we can embed chat logs with the proper styles/images if desired
	if(istype(SSassets.transport, /datum/asset_transport/webroot))
		if(isnull(webroot_asset_urls))
			webroot_asset_urls = list()
			for(var/asset_type in list(/datum/asset/simple/tgui_panel, /datum/asset/simple/namespaced/fontawesome, /datum/asset/simple/namespaced/tgfont, /datum/asset/spritesheet_batched/chat))
				var/datum/asset/asset = get_asset_datum(asset_type)
				webroot_asset_urls += asset.get_url_mappings()
		metadata["webroot"] = list(
			"base_url" = CONFIG_GET(string/asset_cdn_url),
			"assets" = webroot_asset_urls,
		)
	window.send_message("metadata", metadata)

/datum/tgui_panel/proc/send_hotkey_mode()
	window.send_message("verbs/hotkey_mode", list("hotkeys" = client.hotkeys))

/datum/tgui_panel/proc/create_browser(layout = TGPANEL_ONMAP, force = FALSE)
	if(force)
		winset(client, "browseroutput", list("parent" = ""))

	log_tgui(client, "create_browser: [current_layout] -> [layout], force=[force]", context = "tgui_panel")

	// Close the popup window if we're leaving window mode
	if(current_layout == TGPANEL_WINDOW && layout != TGPANEL_WINDOW)
		client << browse(null, "window=[TGPANEL_POPUP_WINDOW]")

	current_layout = layout

	switch(layout)
		if(TGPANEL_ONMAP)
			winset(client, "browseroutput", list(
				"parent" = "mapwindow",
				"type" = "BROWSER",
				"background-color" = "none",
				"inner-background-color" = "transparent",
			))
			winset(client, "split", list("right" = ""))

		if(TGPANEL_PANEL)
			winset(client, "browseroutput", list(
				"parent" = "output_browser",
				"type" = "BROWSER",
				"pos" = "0,0",
				"size" = "640x456",
				"anchor1" = "0,0",
				"anchor2" = "100,100",
			))
			winset(client, "split", list("right" = "info_and_buttons"))

		if(TGPANEL_WINDOW)
			client << browse("<html><head><title>Chat</title></head><body style='margin:0;background:#202020'></body></html>", "window=[TGPANEL_POPUP_WINDOW];size=640x456;can_close=0;can_resize=0;titlebar=0")
			winset(client, TGPANEL_POPUP_WINDOW, list("background-color" = "#202020"))
			winset(client, "browseroutput", list(
				"parent" = TGPANEL_POPUP_WINDOW,
				"type" = "BROWSER",
				"pos" = "0,0",
				"size" = "640x456",
				"anchor1" = "0,0",
				"anchor2" = "100,100",
			))
			winset(client, "split", list("right" = ""))

	client.view_size?.setDefault(VIEWPORT_USE_PREF)

	if(force)
		initialize(force = TRUE)
